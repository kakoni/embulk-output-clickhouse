plugins {
    id 'com.jfrog.bintray' version '1.6'
    id 'com.github.jruby-gradle.base' version '1.2.1'
    id 'java'
    id 'checkstyle'
}
import com.github.jrubygradle.JRubyExec
repositories {
    mavenCentral()
    jcenter()
    maven {
        url "http://dl.bintray.com/embulk-output-jdbc/maven"
    }
}
configurations {
    provided
    defaultJdbcDriver
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

version = "0.1.4"

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile  "org.embulk:embulk-core:0.9.15"
    provided "org.embulk:embulk-core:0.9.15"

    compile "org.embulk.output.jdbc:embulk-output-jdbc:0.8.7"
    compile "ru.yandex.clickhouse:clickhouse-jdbc:0.2.4"
    defaultJdbcDriver "ru.yandex.clickhouse:clickhouse-jdbc:0.2.4"

    testCompile "junit:junit:4.+"
    testCompile "org.embulk:embulk-core:0.9.15:tests"
    testCompile "org.embulk:embulk-standards:0.9.15"
}

task classpath(type: Copy, dependsOn: ["jar"]) {
    doFirst { file('classpath').deleteDir() }
    from (configurations.runtime - configurations.provided - configurations.defaultJdbcDriver + files(jar.archivePath))
    into 'classpath'
}
task defaultJdbcDriver(type: Copy, dependsOn: ["jar"]) {
    doFirst { file('default_jdbc_driver').deleteDir() }
    from (configurations.defaultJdbcDriver)
    into 'default_jdbc_driver'
}
clean {
    delete 'classpath'
    delete 'default_jdbc_driver'
}
checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}
task gem(type: JRubyExec, dependsOn: ['build', 'gemspec', 'classpath', 'defaultJdbcDriver']) {
    jrubyArgs '-rrubygems/gem_runner', "-eGem::GemRunner.new.run(ARGV)", 'build'
    scriptArgs "build/gemspec"
    doLast { ant.move(file: "${project.name}-${project.version}.gem", todir: "pkg") }
}

task gemspec << {
    file('build').mkdirs();
    file('build/gemspec').write($/
Gem::Specification.new do |spec|
  spec.name          = "${project.name}"
  spec.version       = "${project.version}"
  spec.authors       = ["Karri NiemelÃ¤"]
  spec.summary       = %[Clickhouse output plugin for Embulk]
  spec.description   = %[Dumps records to Clickhouse.]
  spec.email         = ["karri.niemela@beans.fi"]
  spec.licenses      = ["MIT"]
  spec.homepage      = "https://github.com/kakoni/embulk-output-clickhouse"

  spec.files         = `git ls-files`.split("\n") + Dir["classpath/*.jar"] + Dir["default_jdbc_driver/*.jar"]
  spec.test_files    = spec.files.grep(%r"^(test|spec)/")
  spec.require_paths = ["lib"]
end
/$)
}
